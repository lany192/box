if (apk.moduleMode) {
    plugins.apply('com.android.library')
} else {
    plugins.apply('com.android.application')
}
plugins.apply('kotlin-android')
plugins.apply('kotlin-kapt')
plugins.apply('kotlin-parcelize')
plugins.apply('dagger.hilt.android.plugin')
plugins.apply('com.alibaba.arouter')

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    namespace apk.appId + '.' + project.getName()
    compileSdk apk.compileSdkVersion

    defaultConfig {
        minSdk apk.minSdkVersion
        targetSdk apk.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        if (!apk.moduleMode) {
            applicationId namespace
            versionCode apk.versionCode
            versionName apk.versionName
        }
    }

    sourceSets {
        main {
            if (apk.moduleMode) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                java.srcDirs = ['src/main/java', 'src/main/develop']
                manifest.srcFile 'src/main/develop/AndroidManifest.xml'
            }
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    packagingOptions {
        resources {
            excludes += ['**/*.properties', '**/*.txt']
            excludes += ['META-INF/*.info', 'META-INF/*.version']
            excludes += ['META-INF/*.md', 'META-INF/*.kotlin_module']
            excludes += ['META-INF/maven/com.squareup.okio/okio/pom.xml', 'META-INF/ASL2.0']
            excludes += ['META-INF/MANIFEST.MF', 'META-INF/NOTICE', 'META-INF/LICENSE']
            excludes += ['META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES', 'META-INF/LGPL2.1']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    androidTestDependencies.each { k, v -> androidTestImplementation v }
    processorDependencies.each { k, v -> kapt v }
    testImplementationDependencies.each { k, v -> testImplementation v }
    implementationDependencies.each { k, v -> implementation v }

    implementation project(path: ':common:router')

    implementation project(path: ':library:arch')
    implementation project(path: ':library:view')
    implementation project(path: ':library:core')
    implementation project(path: ':library:dialog')
    implementation project(path: ':library:update')
    implementation project(path: ':library:time')
    implementation project(path: ':library:crop')
    implementation project(path: ':library:matisse')
    implementation project(path: ':library:permission')
    implementation project(path: ':library:tablayout')
    implementation project(path: ':library:html')
    implementation project(path: ':library:scanner')
    implementation project(path: ':library:cropper')
    implementation project(path: ':library:umeng')
    implementation project(path: ':library:log')
}