if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.alibaba.arouter'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    namespace apk.appId + '.' + project.getName()
    compileSdk apk.compileSdkVersion

    defaultConfig {
        minSdk apk.minSdkVersion
        targetSdk apk.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        if (isModule.toBoolean()) {
            applicationId namespace
            versionCode apk.versionCode
            versionName apk.versionName
            multiDexEnabled true
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                java.srcDirs = ['src/main/java', 'src/develop/java']
                res.srcDirs = ['src/main/res', 'src/develop/res']
                manifest.srcFile 'src/develop/AndroidManifest.xml'
            } else {
                java.srcDirs = ['src/main/java']
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['**/*.properties', '**/*.txt']
            excludes += ['META-INF/*.info', 'META-INF/*.version']
            excludes += ['META-INF/*.md', 'META-INF/*.kotlin_module']
            excludes += ['META-INF/maven/com.squareup.okio/okio/pom.xml', 'META-INF/ASL2.0']
            excludes += ['META-INF/MANIFEST.MF', 'META-INF/NOTICE', 'META-INF/LICENSE']
            excludes += ['META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES', 'META-INF/LGPL2.1']
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    kapt libs.glide.compiler
    kapt libs.androidx.room.compiler
    kapt libs.hilt.compiler
    kapt libs.arouter.compiler

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit2
    androidTestImplementation libs.androidx.test.espresso

    implementation project(path: ':common:router')
    implementation project(path: ':common:network')

    implementation project(path: ':library:arch')
    implementation project(path: ':library:view')
    implementation project(path: ':library:core')
    implementation project(path: ':library:dialog')
    implementation project(path: ':library:update')
    implementation project(path: ':library:time')
    implementation project(path: ':library:crop')

    implementation project(path: ':library:tablayout')
    implementation project(path: ':library:html')
    implementation project(path: ':library:scanner')
    implementation project(path: ':library:cropper')

    implementation project(path: ':library:log')

    implementation libs.androidx.appcompat
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.multidex
    implementation libs.androidx.material
    implementation libs.androidx.exifinterface
    implementation libs.androidx.paging.runtime.ktx
    implementation libs.androidx.paging.runtime
    implementation libs.androidx.startup.runtime
    implementation libs.androidx.media3.exoplayer
    implementation libs.androidx.media3.exoplayer.dash
    implementation libs.androidx.media3.ui
    implementation libs.androidx.camera.core
    implementation libs.androidx.camera.camera2
    implementation libs.androidx.camera.lifecycle
    implementation libs.androidx.camera.view
    implementation libs.androidx.camera.extensions
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    //#协程 https://github.com/Kotlin/kotlinx.coroutines
    implementation libs.coroutines.core
    implementation libs.coroutines.android
    //注入依赖 https://dagger.dev/hilt/
    implementation libs.hilt.android
    //Toast工具 https://github.com/getActivity/Toaster
    implementation libs.toaster
    //适配器 https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    implementation libs.brvah
    //状态栏工具 https://github.com/gyf-dev/ImmersionBar
    implementation libs.immersionbar
    //请求框架 https://github.com/square/retrofit
    implementation libs.retrofit
    implementation libs.retrofit.moshi
    implementation libs.retrofit.gson

    implementation libs.moshi

    //tencent 存储 https://github.com/Tencent/MMKV
    implementation libs.tencent.mmkv
    //tencent 日志工具 https://github.com/Tencent/mars
    implementation libs.tencent.xlog
    //https://github.com/Tencent/VasSonic
    implementation libs.tencent.sonic
    //微信SDK https://developers.weixin.qq.com/doc/oplatform/Downloads/Android_Resource.html
    implementation libs.tencent.wechat
    //腾讯x5 https://x5.tencent.com/docs/index.html
    implementation libs.tencent.tbs
    //多渠道打包工具 https://github.com/Tencent/VasDolly
    implementation libs.tencent.vasdolly.reader
    implementation libs.tencent.vasdolly.writer
    //glide依赖 https://github.com/bumptech/glide
    implementation libs.glide
    //异常捕获 https://github.com/iqiyi/xCrash
    implementation libs.xcrash
    //刷新套件 https://github.com/scwang90/SmartRefreshLayout
    implementation libs.refresh.header
    implementation libs.refresh.layout
    //阿里界面路由 https://github.com/alibaba/ARouter
    implementation libs.arouter.api
    //背景控件。建议配置属性自动提示，详情见 https://github.com/JavaNoober/BackgroundLibrary
    implementation libs.backgroundx
    //图片压缩 https://github.com/forJrking/KLuban
    implementation libs.kluban
    //图片压缩 https://github.com/Curzibn/Luban
    implementation libs.luban
    //RecyclerView分割线 https://github.com/lany192/ItemDecoration
    implementation libs.decoration
    //键盘监听器 https://github.com/lany192/KeyboardWatcher
    implementation libs.keyboard
    //flexbox控件 https://github.com/google/flexbox-layout
    implementation libs.flexbox
    //自定义样式的NumberPicker https://github.com/ShawnLin013/NumberPicker
    implementation libs.number.picker
    //通知事件 https://github.com/greenrobot/EventBus
    implementation libs.eventbus
    //下载框架 https://github.com/lany192/okdownload
    implementation libs.okdownload
    //开源协议对话框
    implementation libs.licensesdialog
    //支付宝打赏 https://github.com/fython/AlipayZeroSdk
    implementation libs.alipay.zero.sdk
    //banner https://github.com/youth5201314/banner
    implementation libs.banner
    //图片缩放控件 https://github.com/MikeOrtiz/TouchImageView
    implementation libs.touchimageview
    //二维码 https://github.com/zxing/zxing
    implementation libs.zxing
    //https://github.com/ReactiveX/RxJava
    implementation libs.rxjava
    //https://github.com/ReactiveX/RxAndroid
    implementation libs.rxandroid
    //https://github.com/jaredrummler/materialspinner
    implementation libs.material.spinner
    //https://github.com/NightWhistler/HtmlSpanner
    implementation libs.html.spanner
    //重启工具库 https://github.com/JakeWharton/ProcessPhoenix
    implementation libs.phoenix
    //minio库 minio 官方的sdk 只有java 版本的不兼容android，可以尝试使用 amazon s3 的oss sdk 来实现功能
    implementation libs.oss.sdk.client
    implementation libs.oss.sdk.s33
    //权限 https://github.com/getActivity/XXPermissions
    implementation libs.permissions
}