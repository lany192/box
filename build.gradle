import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    dependencies {
        classpath libs.arouter.register
    }
}

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.kotlin.kapt) apply false
    alias(libs.plugins.hilt.android) apply false
}

allprojects {
    //配置说明 https://juejin.cn/post/6908232077200588814
    configurations.all { Configuration c ->
        // 动态版本缓存时效
        resolutionStrategy.cacheDynamicVersionsFor(10, "minutes")
        // 快照版本缓存时效
        resolutionStrategy.cacheChangingModulesFor(4, "hours")
        // 所有的依赖降级策略
        c.resolutionStrategy {
            dependencySubstitution {
                all { DependencySubstitution dependency ->
                    if (dependency.requested instanceof ModuleComponentSelector) {
                        // 如果发现项目内存在 group +name 等于远端的group+name  那么直接采用本地工程进行编译
                        def p = rootProject.allprojects.find { p -> p.group == dependency.requested.group && p.name == dependency.requested.module }
                        if (p != null) {
                            println "选择本地模块：" + p.name
                            dependency.useTarget(project(p.path), '选择本地模块')
                        }
                    }
                }
            }
        }
    }
    afterEvaluate {
        if (project.plugins.hasPlugin('com.android.library') || project.plugins.hasPlugin('com.android.application')) {
            def android = project.extensions.getByName('android')
            android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
            android.kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8
            }
        }
        tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_1_8
            }
        }
    }
}

allprojects {
    ext {
        // 安装包
        apk = [
                appId            : 'com.lany192.box',
                groupId          : 'com.github.lany192.box',
                versionCode      : 1,
                versionName      : '1.0.0',
                minSdkVersion    : 21,
                compileSdkVersion: 34,
                targetSdkVersion : 30
        ]
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// 项目构建之前
gradle.settingsEvaluated {
    println "初始化阶段0 settingsEvaluated"
}
gradle.projectsLoaded {
    println "初始化阶段1 projectsLoaded"
}
// 配置阶段
gradle.beforeProject {
    println "配置阶段0 beforeProject"
}
gradle.afterProject {
    println "配置阶段1 afterProject"
}
gradle.projectsEvaluated {
    println "配置阶段2 projectsEvaluated"
}
gradle.taskGraph.whenReady {
    println "配置阶段3 taskGraph.whenReady"
}
