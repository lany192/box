plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
//        arg("AROUTER_GENERATE_DOC", "enable")
        //是否debug模式
        arg("ROUTER_DEBUG", true)
        //是否打印JS路由文档
        arg("JS_ROUTER_DOC", true)
        //Uri Scheme标识
        arg("ROUTER_SCHEME", "box")
        //JS路由调用方法
        arg("ROUTER_JS_FUN", "window.app.route")
    }
}

android {
    compileSdk apk.compileSdkVersion

    //签名配置
    signingConfigs {
        config {
            keyAlias 'box'
            keyPassword 'box2022'
            storeFile file('box.key')
            storePassword 'box2022'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    defaultConfig {
        applicationId apk.appId
        minSdk apk.minSdkVersion
        targetSdk apk.targetSdkVersion
        versionCode apk.versionCode
        versionName apk.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
        renderscriptTargetApi apk.minSdkVersion
        vectorDrawables {
            useSupportLibrary true
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        develop {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            matchingFallbacks = ['release']
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            versionNameSuffix '_dev'
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.config
            versionNameSuffix '_dev'
        }
    }
    compileOptions {
//        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.txt', 'META-INF/*.version', 'META-INF/*.md', 'META-INF/*.kotlin_module', 'META-INF/maven/com.squareup.okio/okio/pom.xml', 'META-INF/maven/com.squareup.okio/okio/pom.properties', 'META-INF/ASL2.0', 'META-INF/MANIFEST.MF', 'META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/INDEX.LIST', 'META-INF/DEPENDENCIES', 'META-INF/LGPL2.1']
        }
    }
    //引入安装包生成配置
    apply from: '../package.gradle'
    namespace 'com.lany192.box.sample'
}

dependencies {
//    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:' + apk.desugar
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    androidTestDependencies.each { k, v -> androidTestImplementation v }
    processorDependencies.each { k, v -> kapt v }
    testImplementationDependencies.each { k, v -> testImplementation v }
    implementationDependencies.each { k, v -> implementation v }

    implementation 'com.github.lany192.box:arch:master-SNAPSHOT'
    implementation 'com.github.lany192.box:view:master-SNAPSHOT'
    implementation 'com.github.lany192.box:core:master-SNAPSHOT'
    implementation 'com.github.lany192.box:dialog:master-SNAPSHOT'
    implementation 'com.github.lany192.box:update:master-SNAPSHOT'
    implementation 'com.github.lany192.box:time:master-SNAPSHOT'
    implementation 'com.github.lany192.box:crop:master-SNAPSHOT'
    implementation 'com.github.lany192.box:matisse:master-SNAPSHOT'
    implementation 'com.github.lany192.box:permission:master-SNAPSHOT'
    implementation 'com.github.lany192.box:tablayout:master-SNAPSHOT'
    implementation 'com.github.lany192.box:link:master-SNAPSHOT'
    implementation 'com.github.lany192.box:html:master-SNAPSHOT'
    implementation 'com.github.lany192.box:richeditor:master-SNAPSHOT'
    implementation 'com.github.lany192.box:webview:master-SNAPSHOT'
    implementation 'com.github.lany192.box:scanner:master-SNAPSHOT'
    implementation 'com.github.lany192.box:cropper:master-SNAPSHOT'
    implementation 'com.github.lany192.box:umeng:master-SNAPSHOT'
}
